buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.3.0'
        classpath 'org.codehaus.groovy:gradle-groovy-android-plugin:0.3.7'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'groovyx.grooid.groovy-android'

if(project.hasProperty('checkClasspathChange')) {
    apply from: 'check-classpath-change.gradle'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "gq.baijie.tryit.grooid"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode version.versionCode
        versionName version.versionName
    }
    buildTypes {
        ['debug', 'release'].each{
            getByName(it) {
                minifyEnabled true
                proguardFile getDefaultProguardFile('proguard-android.txt')
                proguardFile 'proguard-rules-groovy.pro'
                proguardFile 'proguard-rules.pro'
                //proguardFile 'proguard-rules-debug.pro'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        // TODO remove this when https://issues.apache.org/jira/browse/GROOVY-7296 resolved
        // groovy runtime library have some references to invalid package on Android
        warning 'InvalidPackage'
    }
}

project.androidGroovy {
    options {
        configure(groovyOptions) {
            encoding = 'UTF-8'
            configurationScript = file "$rootDir/config/groovy/compiler-config.groovy"
        }
        sourceCompatibility = '1.7'
        targetCompatibility = '1.7'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'org.codehaus.groovy:groovy:2.4.5:grooid'
    compile (project(':domain')) {
        // use the 'grooid' classifier of groovy library as above
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
